import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import qs
import qs.modules.common
import qs.modules.common.widgets
import qs.services

Item {
    id: root

    property int currentTab: 0
    property var tabButtonList: [
        {
            "name": "Pomodoro",
            "icon": "󰦖"
        },
        {
            "name": "Stopwatch",
            "icon": "󱎫"
        }
    ]

    // These are keybinds for stopwatch and pomodoro
    Keys.onPressed: event => {
        if ((event.key === Qt.Key_PageDown || event.key === Qt.Key_PageUp) && event.modifiers === Qt.NoModifier) {
            // Switch tabs
            if (event.key === Qt.Key_PageDown)
                currentTab = Math.min(currentTab + 1, root.tabButtonList.length - 1);
            else if (event.key === Qt.Key_PageUp)
                currentTab = Math.max(currentTab - 1, 0);
            event.accepted = true;
        } else if (event.key === Qt.Key_Space || event.key === Qt.Key_S) {
            // Pause/resume with Space or S
            if (currentTab === 0)
                TimerService.togglePomodoro();
            else
                TimerService.toggleStopwatch();
            event.accepted = true;
        } else if (event.key === Qt.Key_R) {
            // Reset with R
            if (currentTab === 0)
                TimerService.resetPomodoro();
            else
                TimerService.stopwatchReset();
            event.accepted = true;
        } else if (event.key === Qt.Key_L) {
            // Record lap with L
            TimerService.stopwatchRecordLap();
            event.accepted = true;
        }
    }

    ColumnLayout {
        anchors.fill: parent
        spacing: 0

        TabBar {
            id: tabBar

            Layout.fillWidth: true
            currentIndex: currentTab
            onCurrentIndexChanged: currentTab = currentIndex

            Repeater {
                model: root.tabButtonList

                delegate: SecondaryTabButton {
                    selected: (index == currentTab)
                    buttonText: modelData.name
                    buttonIcon: modelData.icon
                }
            }

            background: Item {
                WheelHandler {
                    onWheel: event => {
                        if (event.angleDelta.y < 0)
                            tabBar.currentIndex = Math.min(tabBar.currentIndex + 1, root.tabButtonList.length - 1);
                        else if (event.angleDelta.y > 0)
                            tabBar.currentIndex = Math.max(tabBar.currentIndex - 1, 0);
                    }
                    acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
                }
            }
        }

        // Tab indicator
        Item {
            id: tabIndicator

            property bool enableIndicatorAnimation: false

            Layout.fillWidth: true
            height: 3

            Connections {
                function onCurrentTabChanged() {
                    tabIndicator.enableIndicatorAnimation = true;
                }

                target: root
            }

            Rectangle {
                id: indicator

                property int tabCount: root.tabButtonList.length
                property real fullTabSize: root.width / tabCount
                property real targetWidth: tabBar.contentItem.children[0].children[tabBar.currentIndex].tabContentWidth

                implicitWidth: targetWidth
                x: tabBar.currentIndex * fullTabSize + (fullTabSize - targetWidth) / 2
                color: Appearance.colors.colPrimary
                radius: Appearance.rounding.full

                anchors {
                    top: parent.top
                    bottom: parent.bottom
                }

                Behavior on x {
                    enabled: tabIndicator.enableIndicatorAnimation
                    animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                }

                Behavior on implicitWidth {
                    enabled: tabIndicator.enableIndicatorAnimation
                    animation: Appearance.animation.elementMove.numberAnimation.createObject(this)
                }
            }
        }

        // Tabbar bottom border
        Rectangle {
            id: tabBarBottomBorder

            Layout.fillWidth: true
            height: 1
            color: Appearance.colors.colOutlineVariant
        }

        SwipeView {
            id: swipeView

            Layout.topMargin: 10
            Layout.fillWidth: true
            Layout.fillHeight: true
            spacing: 10
            clip: true
            currentIndex: currentTab
            onCurrentIndexChanged: {
                tabIndicator.enableIndicatorAnimation = true;
                currentTab = currentIndex;
            }

            // Tabs
            PomodoroTimer {}

            Stopwatch {}
        }
    }
}
